import grails.plugin.springsecurity.CreateReleaseDropDownTask
import grails.plugin.springsecurity.SoftwareVersion
import org.asciidoctor.gradle.jvm.AsciidoctorTask

apply plugin: 'org.asciidoctor.jvm.convert'

def softwareVersion = SoftwareVersion.build(version.toString())

def asciidoctorAttributes = [
        stableversion        : softwareVersion.stableVersion,
        snapshotversion      : softwareVersion.snapshotVersion,
        copyright            : 'Apache License, Version 2.0',
        docinfo1             : 'true',
        doctype              : 'book',
        encoding             : 'utf-8',
        icons                : 'font',
        id                   : name + ':' + version,
        idprefix             : '',
        idseparator          : '-',
        lang                 : 'en',
        linkattrs            : true,
        numbered             : '',
        producer             : 'Asciidoctor',
        revnumber            : version,
        setanchors           : true,
        'source-highlighter' : 'prettify',
        toc                  : 'left',
        toc2                 : '',
        toclevels            : '2'
]

tasks.named('asciidoctor', AsciidoctorTask) {
    sourceDir layout.projectDirectory.file('src/docs')
    sources { include 'index.adoc' }
    outputDir = layout.buildDirectory.file('docs')
    attributes asciidoctorAttributes
}

tasks.register('docs') {
    group = 'documentation'
    dependsOn = ['groovydoc', 'asciidoctor']
    finalizedBy 'copyImages', 'createReleaseDropDown', 'ghPagesRootIndexPage'
}

tasks.register('copyImages', Copy) {
    group = 'documentation'
    dependsOn = ['docs']
    from layout.projectDirectory.dir('src/docs')
    into layout.buildDirectory.dir('docs')
    include '**/*.png'
    includeEmptyDirs = false
}

tasks.register('ghPagesRootIndexPage', Copy) {
    group = 'documentation'
    dependsOn = ['docs']
    from layout.projectDirectory.file('src/docs/index.tmpl')
    into layout.buildDirectory.dir('docs')
    rename 'index.tmpl', 'ghpages.html'
}

tasks.register('createReleaseDropDown', CreateReleaseDropDownTask) {
    group = 'documentation'
    dependsOn = ['docs']
    mustRunAfter = ['docs']
    githubSlug = 'grails/grails-spring-security-core'
    currentVersion = version
    versions = docsVersionSelectorInclude.split(',').collect { it.strip() }
    index = layout.buildDirectory.file('docs/index.html')
}