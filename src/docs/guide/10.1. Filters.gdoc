There are a few different approaches to configuring filter chain(s). The default way is to use configuration attributes to determine which extra filters to use (e.g. Basic Auth, Switch User, etc.) and add these to the 'core' filters. For example, setting @grails.plugins.springsecurity.useSwitchUserFilter = true@ adds @switchUserProcessingFilter@ to the filter chain (and in the correct order). The filter chain built here is applied to all URLs, so if you need more flexibility then you further refine it using @filterChain.chainMap@ as discussed below.

h4. filterNames

To define custom filters, remove a core filter from the chain (not recommended), or otherwise have control over the filter chain, you can specify the @filterNames@ property as a list of strings, e.g.

{code}
grails.plugins.springsecurity.filterNames = [
   'httpSessionContextIntegrationFilter', 'logoutFilter', 'authenticationProcessingFilter',
   'myCustomProcessingFilter', 'rememberMeProcessingFilter', 'anonymousProcessingFilter',
   'exceptionTranslationFilter', 'filterInvocationInterceptor'
]
{code}

This will create a filter chain corresponding to the Spring beans with the specified names. As with the default approach, the filter chain built here is applied to all URLs.

h4. chainMap

You can also define which filters to applied to different URL patterns using the @filterChain.chainMap@ attribute. This involves defining a Map which specifies one or more lists of filter bean names, each with a corresponding URL pattern, e.g.:

{code}
grails.plugins.springsecurity.filterChain.chainMap = [
   '/urlpattern1/**': 'filter1,filter2,filter3,filter4',
   '/urlpattern2/**': 'filter1,filter3,filter5',
   '/**': 'JOINED_FILTERS',
]
{code}

In this example, four filters are applied to URLs matching @/urlpattern1/\*\*@ and three different filters are applied to URLs matching @/urlpattern2/\*\*@. In addition the special token @JOINED_FILTERS@ is applied to all URLs. This is a conventient way to specify that all defined filters (configured either with configuration rules like @useSwitchUserFilter@ or explicitly using @filterNames@) should apply to this pattern.

Note that the order of the mappings is important. Each URL will be tested in order from top to bottom to find the first matching one. So we need a @/\*\*@ catch-all rule at the end for URLs that aren't don't match one of the earlier rules.

h4. clientRegisterFilter

An alternative to setting the @filterNames@ property is @org.codehaus.groovy.grails.plugins.springsecurity.SpringSecurityUtils.clientRegisterFilter()@. This allows you to add a custom filter to the chain at a specified position. Each of the standard filters has a corresponding position in the chain (see @org.codehaus.groovy.grails.plugins.springsecurity.SecurityFilterPosition@ for details). So if you have created an application-specific filter, register it in @grails-app/conf/spring/resources.groovy@:

{code}
beans = {
   myFilter(com.mycompany.myapp.MyFilter) {
      // properties
   }
}
{code}

and then register it in @grails-app/conf/BootStrap.groovy@:

{code}
import org.codehaus.groovy.grails.plugins.springsecurity.SecurityFilterPosition
import org.codehaus.groovy.grails.plugins.springsecurity.SpringSecurityUtils

class BootStrap {

   def init = { servletContext ->
      SpringSecurityUtils.clientRegisterFilter(
          'myFilter', SecurityFilterPosition.OPENID_FILTER.order + 10)
   }
} 
{code}

This bootstrap code will register your filter just after the Open ID filter (if it's configured). You cannot register a filter in the same position as another, so it's a good idea to add a small delta to its position to put it after or before a filter that it should be next to in the chain. Note that the Open ID filter position is just an example - add your filter in whatever position makes sense.
