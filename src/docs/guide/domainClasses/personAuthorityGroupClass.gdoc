The typical approach to mapping the relationship between 'person' and 'group' is a many-to-many. In a standard implementation, users have multiple roles, and roles are shared by multiple users. In a group implementation, users have multiple groups, and groups are shared by multiple users. For the same reason we would use a join class between 'person' and 'authority', we should use one between 'person' and 'group'. Please note that when using groups, there should not be a join class between 'person' and 'authority', since 'group' resides between the two.

If you run the [s2-quickstart|Scripts] script with the group name specified, this class will be generated for you, so you don't need to deal with the details of mapping it. Assuming you choose @com.mycompany.myapp@ as your package, and @User@ and @RoleGroup@ as your class names, you'll generate this class:

{code}
package com.mycompany.myapp

import org.apache.commons.lang.builder.HashCodeBuilder

class UserRoleGroup implements Serializable {

   private static final long serialVersionUID = 1

   User user
   RoleGroup roleGroup

   boolean equals(other) {
      if (!(other instanceof UserRoleGroup)) {
         return false
      }

      other.user?.id == user?.id &&
      other.roleGroup?.id == roleGroup?.id
   }

   int hashCode() {
      def builder = new HashCodeBuilder()
      if (user) builder.append(user.id)
      if (roleGroup) builder.append(roleGroup.id)
      builder.toHashCode()
   }

   static UserRoleGroup get(long userId, long roleGroupId) {
      UserRoleGroup.where {
         user == User.load(userId) &&
         roleGroup == RoleGroup.load(roleGroupId)
      }.get()
   }

   static boolean exists(long userId, long roleGroupId) {
      UserRoleGroup.where {
         user == User.load(userId) &&
         roleGroup == RoleGroup.load(roleGroupId)
      }.count() > 0
   }

   static UserRoleGroup create(User user, RoleGroup roleGroup, boolean flush = false) {
      def instance = new UserRoleGroup(user: user, roleGroup: roleGroup)
      instance.save(flush: flush, insert: true)
      instance
   }

   static boolean remove(User u, RoleGroup g) {
      if (u == null || g == null) return false

      int rowCount = UserRoleGroup.where {
         user == User.load(u.id) &&
         roleGroup == RoleGroup.load(g.id)
      }.deleteAll()

      rowCount > 0
   }

   static void removeAll(User u) {
      if (u == null) return

      UserRoleGroup.where {
         user == User.load(u.id)
      }.deleteAll()
   }

   static void removeAll(RoleGroup g) {
      if (g == null) return

      UserRoleGroup.where {
         roleGroup == RoleGroup.load(g.id)
      }.deleteAll()
   }

   static constraints = {
      user validator: { User u, UserRoleGroup ug ->
         if (ug.roleGroup == null) return
         boolean existing = false
         UserRoleGroup.withNewSession {
            existing = UserRoleGroup.exists(u.id, ug.roleGroup.id)
         }
         if (existing) {
            return 'userGroup.exists'
         }
      }
   }

   static mapping = {
      id composite: ['roleGroup', 'user']
      version false
   }
}
{code}
