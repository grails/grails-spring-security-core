The plugin supplies an @\@Secured@ annotation that you can use in your controllers to configure which roles are required for which actions.

You can define the annotation at the class level, meaning that the specified roles are required for all actions, or at the action level, or both. If the class and an action are annotated then the action annotation values will be used since they're more specific.

For example, given this controller:

{code}
package com.mycompany.myapp

import grails.plugins.springsecurity.Secured

class SecureAnnotatedController {

   @Secured(['ROLE_ADMIN'])
   def index = {
      render 'you have ROLE_ADMIN'
   }

   @Secured(['ROLE_ADMIN', 'ROLE_SUPERUSER'])
   def adminEither = {
      render 'you have ROLE_ADMIN or SUPERUSER'
   }

   def anybody = {
      render 'anyone can see this'
   }
}
{code}

you'd need to be authenticated and have @ROLE_ADMIN@ to see @/myapp/secureAnnotated@ (or @/myapp/secureAnnotated/index@) and be authenticated and have @ROLE_ADMIN@ or @ROLE_SUPERUSER@ to see @/myapp/secureAnnotated/adminEither@. Any user can access @/myapp/secureAnnotated/anybody@.

Quite often most actions in a controller require similar access rules, so you can also define annotations at the class level:

{code}
package com.mycompany.myapp

import grails.plugins.springsecurity.Secured

@Secured(['ROLE_ADMIN'])
class SecureClassAnnotatedController {

   def index = {
      render 'index: you have ROLE_ADMIN'
   }

   def otherAction = {
      render 'otherAction: you have ROLE_ADMIN'
   }

   @Secured(['ROLE_SUPERUSER'])
   def super = {
      render 'super: you have ROLE_SUPERUSER'
   }
}
{code}

Here you'd need to be authenticated and have @ROLE_ADMIN@ to see @/myapp/secureClassAnnotated@ (or @/myapp/secureClassAnnotated/index@) or @/myapp/secureClassAnnotated/otherAction@. However you must have @ROLE_SUPERUSER@ to access @/myapp/secureClassAnnotated/super@ - the action-scope annotation overrides the class-scope annotation.

h4. controllerAnnotations.staticRules

You can also define 'static' mappings that cannot be expressed in the controllers, such as '/**' or for JavaScript, CSS, or image URLs. Use the @controllerAnnotations.staticRules@ property, e.g.

{code}
grails.plugins.springsecurity.controllerAnnotations.staticRules = [
   '/js/admin/**': ['ROLE_ADMIN'],
   '/somePlugin/**': ['ROLE_ADMIN']
]
{code}

In this example we've mapped all URLs associated with 'somePlugin' to @ROLE_ADMIN@; annotations aren't an option here since you wouldn't want to edit plugin code for a change like this.
