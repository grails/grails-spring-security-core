Hopefully the default configuraton plus the configurability exposed in @DefaultSecurityConfig.groovy@ and @grails-app/conf/Config.groovy@ enable most customization needs for your applications. However security is a large topic and there are many possible ways to secure an application.

When authenticating users from a database using [DaoAuthenticationProvider|http://static.springsource.org/spring-security/site/docs/3.0.x/apidocs/org/springframework/security/authentication/dao/DaoAuthenticationProvider.html] (the default mode in the plugin if you haven't enabled OpenID, LDAP, etc.), an implementation of [UserDetailsService|http://static.springsource.org/spring-security/site/docs/3.0.x/apidocs/org/springframework/security/core/userdetails/UserDetailsService.html] is required. This class is responsible for returning a concrete implementation of [UserDetails|http://static.springsource.org/spring-security/site/docs/3.0.x/apidocs/org/springframework/security/core/userdetails/UserDetails.html]. The plugin provides @org.codehaus.groovy.grails.plugins.springsecurity.GormUserDetailsService@ as its @UserDetailsService@ implementation and Spring Security's [User|http://static.springsource.org/spring-security/site/docs/3.0.x/apidocs/org/springframework/security/core/userdetails/User.html] as its @UserDetails@ implementation.

You can extend or replace @GormUserDetailsService@ with your own implementation by defining a bean in @grails-app/conf/spring/resources.groovy@ (or @resources.xml@) with the same bean name, @userDetailsService@. This works because application beans are configured after plugin beans and there can only be one bean for each name.

Here's an example @UserDetails@ and @UserDetailsService@ implementation that maintains the primary key (id) and the full name of the user in addition to the standard information. If you extract extra data from your domain class, you'll be less likely to need to reload the user from the database - most of your common data can be kept along with your security credentials.

In this example I'm adding in a @fullName@ field and an @id@ field. Keeping the full name cached avoids hitting the database just for that lookup, but by also caching the id we can do a more efficient database load of the user. If all you have is the username, then you need to call @User.findByUsername(principal.username)@, but if you have the id you can call @User.get(principal.id)@. Even if you have a unique index on the @username@ database column, loading by primary key will usually be more efficient since it can take advantage of Hibernate's first-level and second-level caches.

There's really not much to implement other than your application-specific lookup code:

{code}
package com.foo.bar

import org.springframework.security.GrantedAuthority
import org.springframework.security.userdetails.User

class MyUserDetails extends User {

   final long id
   final String fullName

   MyUserDetails(String username, String password, boolean enabled,
                 boolean accountNonExpired, boolean credentialsNonExpired,
                 boolean accountNonLocked,
                 Collection<GrantedAuthority> authorities,
                 long id, String fullName) {
      super(username, password, enabled, accountNonExpired,
            credentialsNonExpired, accountNonLocked, authorities)

      this.id = id
      this.fullName = fullName
   }
}
{code}

{code}
package com.foo.bar

import org.springframework.security.userdetails.UserDetails
import org.springframework.security.userdetails.UserDetailsService

class MyUserDetailsService implements UserDetailsService {

   UserDetails loadUserByUsername(String username) throws UsernameNotFoundException {
      // lookup user and data
      return new MyUserDetails(username, password, enabled,
                               accountNonExpired, credentialsNonExpired,
                               accountNonLocked, authorities, id, fullName)
   }
}
{code}

and to use it, register it in @grails-app/conf/spring/resources.groovy@ like this:

{code}
beans = {
   userDetailsService(com.foo.bar.MyUserDetailsService)
}
{code}

Another option if you want to load users and roles from the database is to subclass @org.codehaus.groovy.grails.plugins.springsecurity.GormUserDetailsService@ - the methods are all protected so you can override whatever you want.

Also note that this approach works with all beans defined in @SpringSecurityCoreGrailsPlugin.doWithSpring()@ - you can replace or subclass any of the Spring beans to provide your own functionality when the standard extension mechanisms aren't sufficient.

h4. Flushing the cached @Authentication@

If you store mutable data in your custom @UserDetails@ implementation (for example full name like in this example), be sure to rebuild the @Authentication@ if it changes. @springSecurityService@ has a @reauthenticate@ method that will do this for you:

{code}
class MyController {

   def springSecurityService

   def someAction {
      def user = ...
      // update user data
      user.save()
      springSecurityService.reauthenticate user.username
      ...
   }
}
{code}

