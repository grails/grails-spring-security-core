Hierarchical roles are a convenient way to reduce clutter in your request mappings.

{table}
*Property* | *Default Value*  | *Meaning*
roleHierarchy | none | Hierarchical role definition.
roleHierarchyEntryClassName | none | Domain class used to manage persistent role hierarchy entries
{table}

For example, if you have several types of 'admin' roles that can be used to access a URL pattern and you do not use hierarchical roles, you need to specify all the admin roles:

{code}
package com.mycompany.myapp

import grails.plugin.springsecurity.annotation.Secured

class SomeController {

   @Secured(['ROLE_ADMIN', 'ROLE_FINANCE_ADMIN', 'ROLE_SUPERADMIN'])
   def someAction() {
      ...
   }
}
{code}

However, if you have a business rule that says @ROLE_FINANCE_ADMIN@ implies being granted @ROLE_ADMIN@, and that @ROLE_SUPERADMIN@ implies being granted @ROLE_FINANCE_ADMIN@, you can express that hierarchy as:

{code}
grails.plugin.springsecurity.roleHierarchy = '''
   ROLE_SUPERADMIN > ROLE_FINANCE_ADMIN
   ROLE_FINANCE_ADMIN > ROLE_ADMIN
'''
{code}

Then you can simplify your mappings by specifying only the roles that are required:

{code}
package com.mycompany.myapp

import grails.plugin.springsecurity.annotation.Secured

class SomeController {

   @Secured(['ROLE_ADMIN'])
   def someAction() {
      ...
   }
}
{code}

You can also reduce the number of granted roles in the database. Where previously you had to grant @ROLE_SUPERADMIN@, @ROLE_FINANCE_ADMIN@, and @ROLE_ADMIN@, now you only need to grant @ROLE_SUPERADMIN@.

h4. Persistent role hierarchy

Specifying a static string in the @roleHierarchy@ property will be sufficient for most applications, but you can also store the information in your database. This is particularly useful if you're also storing requestmaps in the database. To use persistant storage, run the [s2-create-role-hierarchy-entry|Scripts] script. This will create the domain class and enable persistent storage by registering its name as the @roleHierarchyEntryClassName@ setting in @grails-app/conf/application.groovy@.

For example, running

{code}
$ grails s2-create-role-hierarchy-entry com.yourapp.RoleHierarchyEntry
{code}

will generate this class in @grails-app/domain/com/yourapp/RoleHierarchyEntry.groovy@:

{code}
package com.yourapp

class RoleHierarchyEntry implements Serializable {

   private static final long serialVersionUID = 1

   String entry

   RoleHierarchyEntry(String entry) {
      this()
      this.entry = entry
   }

   @Override
   int hashCode() {
      entry?.hashCode() ?: 0
   }

   @Override
   boolean equals(other) {
      is(other) || (other instanceof RoleHierarchyEntry && other.entry == entry)
   }

   @Override
   String toString() {
      entry
   }

   static constraints = {
      entry blank: false, unique: true
   }

   static mapping = {
      cache true
   }
}
{code}

To store the equivalent entries for the ROLE_SUPERADMIN / ROLE_FINANCE_ADMIN / ROLE_ADMIN hierarchy, add code like this to a method in a transactional service:

{code}
if (!RoleHierarchyEntry.count()) {
   new RoleHierarchyEntry('ROLE_SUPERADMIN > ROLE_FINANCE_ADMIN').save()
   new RoleHierarchyEntry('ROLE_FINANCE_ADMIN > ROLE_ADMIN').save()
}
{code}
