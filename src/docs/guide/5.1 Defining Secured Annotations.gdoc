You can use an @\@Secured@ annotation in your controllers to configure which roles are required for which actions. To use annotations, specify @securityConfigType="Annotation"@, or leave it unspecified because it's the default:

{code}
grails.plugins.springsecurity.securityConfigType = "Annotation"
{code}

You can define the annotation at the class level, meaning that the specified roles are required for all actions, or at the action level, or both. If the class and an action are annotated then the action annotation values will be used since they're more specific.

For example, given this controller:

{code}
package com.mycompany.myapp

import grails.plugins.springsecurity.Secured

class SecureAnnotatedController {

   @Secured(['ROLE_ADMIN'])
   def index = {
      render 'you have ROLE_ADMIN'
   }

   @Secured(['ROLE_ADMIN', 'ROLE_SUPERUSER'])
   def adminEither = {
      render 'you have ROLE_ADMIN or SUPERUSER'
   }

   def anybody = {
      render 'anyone can see this'
   }
}
{code}

you need to be authenticated and have @ROLE_ADMIN@ to see @/myapp/secureAnnotated@ (or @/myapp/secureAnnotated/index@) and be authenticated and have @ROLE_ADMIN@ or @ROLE_SUPERUSER@ to see @/myapp/secureAnnotated/adminEither@. Any user can access @/myapp/secureAnnotated/anybody@.

Often most actions in a controller require similar access rules, so you can also define annotations at the class level:

{code}
package com.mycompany.myapp

import grails.plugins.springsecurity.Secured

@Secured(['ROLE_ADMIN'])
class SecureClassAnnotatedController {

   def index = {
      render 'index: you have ROLE_ADMIN'
   }

   def otherAction = {
      render 'otherAction: you have ROLE_ADMIN'
   }

   @Secured(['ROLE_SUPERUSER'])
   def super = {
      render 'super: you have ROLE_SUPERUSER'
   }
}
{code}

Here you need to be authenticated and have @ROLE_ADMIN@ to see @/myapp/secureClassAnnotated@ (or @/myapp/secureClassAnnotated/index@) or @/myapp/secureClassAnnotated/otherAction@. However, you must have @ROLE_SUPERUSER@ to access @/myapp/secureClassAnnotated/super@. The action-scope annotation overrides the class-scope annotation.

h4. controllerAnnotations.staticRules

You can also define 'static' mappings that cannot be expressed in the controllers, such as '/\*\*' or for JavaScript, CSS, or image URLs. Use the @controllerAnnotations.staticRules@ property, for example:

{code}
grails.plugins.springsecurity.controllerAnnotations.staticRules = [
   '/js/admin/**': ['ROLE_ADMIN'],
   '/someplugin/**': ['ROLE_ADMIN']
]
{code}

This example maps all URLs associated with @SomePluginController@, which has URLs of the form @/somePlugin/...@, to @ROLE_ADMIN@; annotations are not an option here because you would not edit plugin code for a change like this.

{note}
When mapping URLs for controllers that are mapped in @UrlMappings.groovy@, you need to secure the un-url-mapped URLs. For example if you have a FooBarController that you map to @/foo/bar/$action@, you must register that in @controllerAnnotations.staticRules@ as @/foobar/\*\*@. This is different than the mapping you would use for the other two approaches and is necessary because @controllerAnnotations.staticRules@ entries are treated as if they were annotations on the corresponding controller.
{note}
