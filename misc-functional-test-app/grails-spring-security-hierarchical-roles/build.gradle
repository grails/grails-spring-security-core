buildscript {
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "com.bertramlabs.plugins:asset-pipeline-gradle:2.14.1"
        classpath "org.grails.plugins:hibernate5:${gormVersion-".RELEASE"}"
    }
}

version "0.1"
group "grails.spring.security.hierarchical.roles"

apply plugin:"eclipse"
apply plugin:"idea"
apply plugin:"war"
apply plugin:"org.grails.grails-web"
apply plugin:"org.grails.grails-gsp"
apply plugin:"asset-pipeline"

repositories {
    mavenLocal()
    maven { url "https://repo.grails.org/grails/core" }
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter-logging"
    compile "org.springframework.boot:spring-boot-autoconfigure"
    compile "org.grails:grails-core"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.springframework.boot:spring-boot-starter-tomcat"
    compile "org.grails:grails-dependencies"
    compile "org.grails:grails-web-boot"
    compile "org.grails.plugins:cache"
    compile "org.grails.plugins:scaffolding"
    compile "org.grails.plugins:hibernate5"
    compile "org.hibernate:hibernate-core:5.1.3.Final"
    compile "org.hibernate:hibernate-ehcache:5.1.3.Final"
    console "org.grails:grails-console"
    profile "org.grails.profiles:web"
    runtime "com.bertramlabs.plugins:asset-pipeline-grails:2.14.1"
    runtime "com.h2database:h2"
    testCompile "org.grails:grails-plugin-testing"
    testCompile "org.grails.plugins:geb"
    testRuntime "org.seleniumhq.selenium:selenium-htmlunit-driver:2.47.1"
    testRuntime "net.sourceforge.htmlunit:htmlunit:2.18"
    testRuntime "org.seleniumhq.selenium:selenium-support:2.53.1"
}

apply from: '../../gradle/ssc.gradle'

bootRun {
    jvmArgs('-Dspring.output.ansi.enabled=always')
    addResources = true
}

assets {
    minifyJs = true
    minifyCss = true
}


integrationTest {
    systemProperties System.properties
}

apply from: '../../gradle/updateGormVersion.gradle'

task s2Quickstart {
    ext.commands = ['s2-quickstart demo User Role', 's2-create-role-hierarchy-entry demo.RoleHierarchyEntry']
    doLast {
        for ( String cmd : commands ) {
            def sout = new StringBuilder(), serr = new StringBuilder()
            def proc = "${project.projectDir}/grailsw $cmd".execute()
            proc.consumeProcessOutput(sout, serr)
            proc.waitForOrKill(20000)
            println "$sout $serr"
        }
    }
}
task resetResourcesGroovy {
    doLast {
        new File("${project.projectDir}/grails-app/conf/spring/resources.groovy").text = 'beans = {\n}'
    }
}
task deleteS2QuickstartGeneratedFiles {
    dependsOn 'resetResourcesGroovy'
    doLast {
	delete("${project.projectDir}/grails-app/conf/application.groovy")
	delete("${project.projectDir}/grails-app/domain/demo")
	delete("${project.projectDir}/src/main")
    }
}
compileGroovy.dependsOn 's2Quickstart', 'updateGormVersion', 'deleteS2QuickstartGeneratedFiles' 
s2Quickstart.mustRunAfter 'deleteS2QuickstartGeneratedFiles'
deleteS2QuickstartGeneratedFiles.mustRunAfter 'updateGormVersion'
